apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  if (project.plugins.findPlugin("com.android.library")) {
    // For Android libraries
    from android.sourceSets.main.java.srcDirs
    // from android.sourceSets.main.kotlin.srcDirs
  } else {
    // For pure Kotlin libraries, in case you have them
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
  }
}

artifacts {
  archives androidSourcesJar
  // archives javadocJar, androidSourcesJar
}

// group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        // The coordinates of the library, being set from variables that
        // we'll set up later
        groupId PUBLISH_GROUP_ID
        artifactId PUBLISH_ARTIFACT_ID
        version PUBLISH_VERSION

        // Two artifacts, the `aar` (or `jar`) and the sources
        if (project.plugins.findPlugin("com.android.library")) {
          from components.release
        } else {
          from components.java
        }

        artifact androidSourcesJar
        // artifact javadocJar

        // Mostly self-explanatory metadata
        pom {
          name = PUBLISH_ARTIFACT_ID
          description = 'A button to substitute the ProgressDialog.'
          url = 'https://github.com/FarhamHosseini/LoadingButton'
          licenses {
            license {
              name = 'Apache License 2.0'
              url = 'https://github.com/FarhamHosseini/LoadingButton/blob/main/LICENSE'
            }
          }
          developers {
            developer {
              id = 'FarhamHosseini'
              name = 'Farham Hosseini'
              email = 'farham.hosseini@apachat.com'
            }
            developer {
              id = 'iRonBotxx'
              name = 'Farshad Hosseini'
              email = 'farshad.hosseini@apachat.com'
            }
          }

          scm {
            connection = 'scm:git:github.com/FarhamHosseini/LoadingButton.git'
            developerConnection = 'scm:git:ssh://github.com/FarhamHosseini/LoadingButton.git'
            url = 'https://github.com/FarhamHosseini/LoadingButton/tree/main'
          }
        }
      }
    }
  }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
  sign publishing.publications
}